group 'ee.ttu.studenttester'
version = '1.4'

apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'ee.ttu.java.studenttester.classes.StudentTesterMain'

sourceCompatibility = 1.9

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.testng/testng
    compile group: 'org.testng', name: 'testng', version: '6.13.1'

    // https://mvnrepository.com/artifact/org.json/json
    compile group: 'org.json', name: 'json', version: '20170516'

    // https://mvnrepository.com/artifact/com.puppycrawl.tools/checkstyle
    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.3'

    // https://mvnrepository.com/artifact/junit/junit
    compile group: 'junit', name: 'junit', version: '4.12'

    // Use TestNG test framework
    testImplementation 'org.testng:testng:6.13'
}

// EXAMPLE: gradle run -Pconf='-contentroot examples/normal/contentRoot -testroot examples/normal/testRoot'

run {
    if (project.hasProperty('conf')) {
        args(conf.split(' '))
    }
}

test {
    useTestNG()
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

jar {
    dependsOn test
    manifest {
        attributes 'Main-Class': mainClassName,
                   'Implementation-Title': 'StudentTester',
                   'Implementation-Version': version + ', build ' + new Date().format('yyyyMMdd_HHmmss'),
                   'Built-By': System.getProperty("user.name")
    }
    from { 
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } } {
        // signatures must be removed to create a fat jar
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}
